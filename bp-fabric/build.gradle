plugins {
    id "fabric-loom" version "0.13-SNAPSHOT" apply false
}

allprojects {
    apply plugin: "fabric-loom"

    repositories {
        maven {
            name = "sonatype-oss-snapshots1"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            mavenContent { snapshotsOnly() }
        }
    }

    dependencies {
        implementation project(":bp-core")

        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    }

    java {
        withSourcesJar()
    }
}

subprojects {
    dependencies {
        implementation(project(path: ":bp-fabric", configuration: "namedElements")) {
            exclude group: "net.fabricmc", module: "fabric-loader" // prevent duplicate fabric-loader on run
        }
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}

// TODO Find a fix for the below
// Requires running ":bp-fabric:1-19:build", ":bp-fabric:1-19-1:build", then ":bp-fabric:build" to build correctly
// Running 1.19 and 1.19.1 builds in the same build (even sequentially) causes the build to run until all memory used

repositories {
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
}

dependencies {
    include project(":bp-core")

    minecraft "com.mojang:minecraft:1.19"
    mappings "net.fabricmc:yarn:1.19+build.4:v2"

    modImplementation include("net.kyori:adventure-platform-fabric:5.5.0-SNAPSHOT")

    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}", {
        exclude module: 'fabric-api'
    }
    include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"

    subprojects.each {
        include project("${it.path}:")
    }
}

java {
    archivesBaseName = rootProject.name + "-Fabric"
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
