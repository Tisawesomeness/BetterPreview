plugins {
    id "fabric-loom" version "0.12-SNAPSHOT" apply false
}

allprojects {
    apply plugin: "fabric-loom"

    repositories {
        maven {
            name = "sonatype-oss-snapshots1"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            mavenContent { snapshotsOnly() }
        }
    }

    dependencies {
        implementation project(":bp-core")

        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

        modImplementation include("net.kyori:adventure-platform-fabric:5.4.0-SNAPSHOT")
    }

    processResources {
        inputs.property "version", project.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }
}

subprojects {
    dependencies {
        implementation(project(path: ":bp-fabric", configuration: "namedElements")) {
            exclude group: "net.fabricmc", module: "fabric-loader" // prevent duplicate fabric-loader on run
        }
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}

dependencies {
    include project(":bp-core")

    minecraft "com.mojang:minecraft:1.19"
    mappings "net.fabricmc:yarn:1.19+build.4:v2"

    subprojects.each {
        include project("${it.path}:")
    }
}

java {
    archivesBaseName = rootProject.name + "-Fabric"
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
